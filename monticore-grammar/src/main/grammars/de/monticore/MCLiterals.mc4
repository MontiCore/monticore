/* (c) https://github.com/MontiCore/monticore */
package de.monticore;

/**
 * This grammar defines Java compliant literals. The scope of this grammar is to
 * ease the reuse of literals structures in Java-like sublanguages, e.g., by 
 * grammar inheritance or grammar embedment.
 * The grammar contains literals from Java, e.g., Boolean, Char, String, ....
*/

component grammar MCLiterals extends de.monticore.MCBasics {

  /*========================================================================*/
  /*======================= AST DEFINITIONS ================================*/
  /*========================================================================*/
  astrule BooleanLiteral = 
    method public boolean getValue() {
      return this.source == ASTConstantsMCLiterals.TRUE;
    }
  ;

  astrule CharLiteral = 
    method public char getValue() {
      return de.monticore.MCLiteralsDecoder.decodeChar(getSource());
    }
  ;

  astrule StringLiteral = 
    method public String getValue() {
      return de.monticore.MCLiteralsDecoder.decodeString(getSource());
    }
  ;

  astrule IntLiteral = 
    method public int getValue() {
      return de.monticore.MCLiteralsDecoder.decodeInt(getSource());
    }
  ;

  astrule SignedIntLiteral = 
    method public int getValue() {
      if (negative) {
        return - de.monticore.MCLiteralsDecoder.decodeInt(getSource());
      }
      return de.monticore.MCLiteralsDecoder.decodeInt(getSource());
    }
  ;
  
   astrule NatLiteral = 
    method public int getValue() {
      return de.monticore.MCLiteralsDecoder.decodeInt(getSource());
    }
  ;

  astrule SignedNatLiteral = 
    method public int getValue() {
      if (negative) {
        return - de.monticore.MCLiteralsDecoder.decodeInt(getSource());
      }
      return de.monticore.MCLiteralsDecoder.decodeInt(getSource());
    }
  ;
 
  astrule LongLiteral = 
    method public long getValue() {
      return de.monticore.MCLiteralsDecoder.decodeLong(getSource());
    }
  ;

  astrule SignedLongLiteral = 
    method public long getValue() {
      if (negative) {
        return -de.monticore.MCLiteralsDecoder.decodeLong(getSource());
      }
      return de.monticore.MCLiteralsDecoder.decodeLong(getSource());
    }
  ;

  astrule FloatLiteral = 
    method public float getValue() {
      return de.monticore.MCLiteralsDecoder.decodeFloat(getSource());
    }
  ;

  astrule SignedFloatLiteral = 
    method public float getValue() {
      if (negative) {
        return -de.monticore.MCLiteralsDecoder.decodeFloat(getSource());
      }
      return de.monticore.MCLiteralsDecoder.decodeFloat(getSource());
    }
  ;

  astrule DoubleLiteral = 
    method public double getValue() {
      return de.monticore.MCLiteralsDecoder.decodeDouble(getSource());
    }
  ;

  astrule SignedDoubleLiteral = 
    method public double getValue() {
      if (negative) {
        return -de.monticore.MCLiteralsDecoder.decodeDouble(getSource());
      }
      return de.monticore.MCLiteralsDecoder.decodeDouble(getSource());
    }
  ;


  /*========================================================================*/
  /*======================= INTERFACE DEFINITIONS ==========================*/
  /*========================================================================*/

  /** ASTLiteral is the interface for all literals (NullLiteral,
        BooleanLiteral, CharLiteral, StringLiteral and all NumericLiterals)
    */
  interface Literal;


  /** ASTSignedLiteral is the interface for all literals (NullLiteral,
        BooleanLiteral, CharLiteral, StringLiteral and all NumericLiterals).
        Compared to Literal it also includes negative NumericLiterals
    */
  interface SignedLiteral;


  /** The interface ASTNumericLiteral combines the numeric literal types for
        Integer, Long, Float and Double
    */
  interface NumericLiteral extends Literal;


  /** The interface ASTNumericLiteral combines the numeric literal types for
        Integer, Long, Float and Double.
        Compared to NumericLiteral it also includes negative numbers.
    */
  interface SignedNumericLiteral extends SignedLiteral;


  /*========================================================================*/
  /*============================ PARSER RULES ==============================*/
  /*========================================================================*/
  
  /** ASTNullLiteral represents 'null'
    */
  NullLiteral implements Literal, SignedLiteral =
    "null";


  /** ASTBooleanLiteral represents "true" or "false"
        @attribute source String-representation (including '"').
    */
  BooleanLiteral implements Literal, SignedLiteral =
    source:["true" | "false"];


  /** ASTCharLiteral represents any valid character parenthesized with "'".
        @attribute source String-representation (including "'").
    */
  CharLiteral implements Literal, SignedLiteral =
    source:Char;


  /** ASTStringLiteral represents any valid character sequence parenthesized
        with '"'.
        @attribute source String-representation (including '"').
    */
  StringLiteral implements Literal, SignedLiteral =
    source:String;

  /** ASTIntLiteral represents a positive Integer number.
        @attribute source String-representation (including '"').
    */
  IntLiteral implements NumericLiteral =
    (source:DecimalIntegerLiteral | source:HexIntegerLiteral | source:OctalIntegerLiteral | source:BinaryIntegerLiteral);


  /** ASTSignedIntLiteral represents a positive or negative Integer number.
        @attribute source String-representation (including '"').
    */
  SignedIntLiteral implements SignedNumericLiteral =
    (negative:["-"])? (source:DecimalIntegerLiteral | source:HexIntegerLiteral | source:OctalIntegerLiteral | source:BinaryIntegerLiteral);


  /** ASTNatLiteral represents a positive Decimal number.
        @attribute source String-representation (including '"').
    */
  NatLiteral implements NumericLiteral =
    source:DecimalIntegerLiteral;

  /** ASTSignedNatLiteral represents a positive or negative Decimal number.
        @attribute source String-representation (including '"').
    */
  SignedNatLiteral implements SignedNumericLiteral =
     (negative:["-"])? source:DecimalIntegerLiteral;

  /** ASTLongLiteral represents a positive Long number.
        @attribute source String-representation (including '"').
    */
  LongLiteral implements NumericLiteral =
    source:Num_Long;


  /** ASTSignedLongLiteral represents a positive or negative Long number.
        @attribute source String-representation (including '"').
    */
  SignedLongLiteral implements SignedNumericLiteral =
    (negative:["-"])? source:Num_Long;


  /** ASTFloatLiteral represents a positive Float number.
        @attribute source String-representation (including '"').
    */
  FloatLiteral implements NumericLiteral =
    source:DecimalFloatingPointLiteral | source:HexadecimalFloatingPointLiteral;


  /** ASTSignedFloatLiteral represents a positive or negative Float number.
        @attribute source String-representation (including '"').
    */
  SignedFloatLiteral implements SignedNumericLiteral =
    (negative:["-"])? (source:DecimalFloatingPointLiteral | source:HexadecimalFloatingPointLiteral);


  /** ASTDoubleLiteral represents a positive Double number.
        @attribute source String-representation (including '"').
    */
  DoubleLiteral implements NumericLiteral =
    source:DecimalDoublePointLiteral | source:HexadecimalDoublePointLiteral;


  /** ASTSignedDoubleLiteral represents a positive or negative Double number.
        @attribute source String-representation (including '"').
    */
  SignedDoubleLiteral implements SignedNumericLiteral =
    (negative:["-"])? (source:DecimalDoublePointLiteral | source:HexadecimalDoublePointLiteral);


  /*========================================================================*/
  /*============================ LEXER RULES ===============================*/
  /*========================================================================*/
  

  /*========================================================================*/
  /* The following section is adapted from                                  */
  /* https://github.com/antlr/grammars-v4/blob/master/java/Java.g4          */
  /*========================================================================*/
  
  // §3.10.1 Integer Literals

  token DecimalIntegerLiteral
    = DecimalNumeral;

  token Num_Long
    = (DecimalIntegerLiteral IntegerTypeSuffix) | (HexIntegerLiteral IntegerTypeSuffix) | (OctalIntegerLiteral IntegerTypeSuffix) | (BinaryIntegerLiteral IntegerTypeSuffix);

  token HexIntegerLiteral
    = HexNumeral;

  token OctalIntegerLiteral
    = OctalNumeral;

  token BinaryIntegerLiteral
    = BinaryNumeral;

  fragment token IntegerTypeSuffix
    = 'l' | 'L';

  fragment token DecimalNumeral
    = '0' | NonZeroDigit ((Digits)? | Underscores Digits);

  fragment token Digits
    = Digit ((DigitOrUnderscore)* Digit)?;

  fragment token Digit
    = '0' | NonZeroDigit;

  fragment token NonZeroDigit
    = '1'..'9' ;

  fragment token DigitOrUnderscore
    = Digit | '_';

  fragment token Underscores
    = '_'+;

  fragment token HexNumeral
    = '0' ('x' | 'X') HexDigits;

  fragment token HexDigits
    = HexDigit ((HexDigitOrUnderscore)* HexDigit)?;

  fragment token HexDigit
    = '0'..'9'  | 'a'..'f'  | 'A'..'F' ;

  fragment token HexDigitOrUnderscore
    = HexDigit | '_';

  fragment token OctalNumeral
    = '0' (Underscores)? OctalDigits;

  fragment token OctalDigits
    = OctalDigit ((OctalDigitOrUnderscore)* OctalDigit)?;

  fragment token OctalDigit
    = '0'..'7' ;

  fragment token OctalDigitOrUnderscore
    = OctalDigit | '_';

  fragment token BinaryNumeral
    = '0' ('b' | 'B') BinaryDigits;

  fragment token BinaryDigits
    = BinaryDigit ((BinaryDigitOrUnderscore)* BinaryDigit)?;

  fragment token BinaryDigit
    = '0' | '1';

  fragment token BinaryDigitOrUnderscore
    = BinaryDigit | '_';


  // §3.10.2 Floating-Point Literals
  
  token DecimalDoublePointLiteral
    = Digits '.' (Digits)? (ExponentPart)? (DoubleTypeSuffix)? {_input.LA(1)!='.'}? | '.' Digits (ExponentPart)? (DoubleTypeSuffix)? | Digits ExponentPart (DoubleTypeSuffix)? | Digits DoubleTypeSuffix;

  token DecimalFloatingPointLiteral
    = Digits '.' (Digits)? (ExponentPart)? (FloatTypeSuffix) | '.' Digits (ExponentPart)? (FloatTypeSuffix) | Digits ExponentPart (FloatTypeSuffix) | Digits FloatTypeSuffix;

  fragment token ExponentPart
    = ExponentIndicator SignedInteger;

  fragment token ExponentIndicator
    = 'e' | 'E';

  fragment token SignedInteger
    = (Sign)? Digits;

  fragment token Sign
    = '+' | '-';

  fragment token FloatTypeSuffix
    = 'f' | 'F';

  fragment token DoubleTypeSuffix
    = 'd' | 'D';

  token HexadecimalDoublePointLiteral
    = HexSignificand BinaryExponent (DoubleTypeSuffix)?;

  token HexadecimalFloatingPointLiteral
    = HexSignificand BinaryExponent (FloatTypeSuffix);

  fragment token HexSignificand
    = HexNumeral '.'? | '0' ('x' | 'X') (HexDigits)? '.' HexDigits;

  fragment token BinaryExponent
    = BinaryExponentIndicator SignedInteger;

  fragment token BinaryExponentIndicator
    = 'p' | 'P';


  // §3.10.4 Character Literals
  token Char
    = '\'' (SingleCharacter|EscapeSequence) '\''
    : {setText(getText().substring(1, getText().length() - 1));};

  fragment token SingleCharacter
    = ~ ('\'');


  // §3.10.5 String Literals
  token String
    = '"' (StringCharacters)? '"'
    : {setText(getText().substring(1, getText().length() - 1));};

  fragment token StringCharacters
    = (StringCharacter)+;

  fragment token StringCharacter
    = ~ ('"' | '\\') | EscapeSequence;

  // §3.10.6 Escape Sequences for Character and String Literals
  fragment token EscapeSequence
    = '\\' ('b' | 't' | 'n' | 'f' | 'r' | '"' | '\'' | '\\') | OctalEscape | UnicodeEscape;

  fragment token OctalEscape
    = '\\' OctalDigit | '\\' OctalDigit OctalDigit | '\\' ZeroToThree OctalDigit OctalDigit;

  fragment token UnicodeEscape
    = '\\' 'u' HexDigit HexDigit HexDigit HexDigit;

  fragment token ZeroToThree
    = '0'..'3' ;

}

