/* (c) https://github.com/MontiCore/monticore */

package de.monticore.expressions;

component grammar JavaClassExpressions extends de.monticore.expressions.CommonExpressions {
	
external ExtLiteral;

external ExtType;

external ExtReturnType;

external ExtTypeArguments;

SuperExpression implements Expression <270> =
	Expression "." "super" SuperSuffix;
	
GenericInvocationExpression implements Expression <260> =
	Expression "." PrimaryGenericInvocationExpression;
	
TypeCastExpression implements Expression <230> =
	"(" ExtType ")" Expression;
        
PrimarySuperExpression implements Expression <330>
	= "super";
	
LiteralExpression implements Expression <340>
    = ExtLiteral;
        
ClassExpression implements Expression <360>
    = ExtReturnType "." "class";
   
PrimaryGenericInvocationExpression implements Expression <370>
    = ExtTypeArguments GenericInvocationSuffix
    ;

GenericInvocationSuffix
    =   ["super"] SuperSuffix
    |   ["this"] Arguments
    |   Name Arguments
    ;
    
SuperSuffix
    =   Arguments
    |   "." ExtTypeArguments? Name Arguments?
    ;
    
NameExpression implements Expression <350>
    = Name;
    
InstanceofExpression implements Expression <140> 
	= Expression "instanceof" ExtType;
}
