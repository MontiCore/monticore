///* (c) https://github.com/MontiCore/monticore */

buildscript {
  if (findProperty('bootstrap')) {
    // fake a different group for Gradle to prevent it from including the build in it self
    group = "de.mc"
  }
}

plugins {
  id "java-library"
  id "com.github.johnrengelman.shadow" version "7.1.2"
  id "monticore" version "$version"
  id "jacoco"
}

description = 'MontiCore: Grammar'
if (!hasProperty('bootstrap')) {
  group = "de.monticore"
} else {
  group = "de.monticore.bs"
}

def grammarOutDir = "$buildDir/generated-sources/monticore/sourcecode"
def testGrammarOutDir = "$buildDir/generated-test-sources/monticore/sourcecode"
def trafoOutDir = "$buildDir/generated-sources/monticore/trafo"
def taggingOutDir = "$buildDir/generated-sources/monticore/tagging"


sourceSets {
  main.java.srcDirs += [grammarOutDir]
  test.java.srcDirs += [testGrammarOutDir]

  grammars {
    java.srcDirs = []
    resources {
      srcDirs([grammarDir, grammarOutDir])
      include "**/*.mc4"
      include "**/*.mlc"
    }
  }
  trafo {
    java.srcDirs = [trafoOutDir]
  }
  tagging {
    java.srcDirs = [taggingOutDir]
  }
}

dependencies {
  api project(':monticore-runtime')
  implementation "com.google.guava:guava:$guava_version"
  implementation "org.apache.commons:commons-lang3:$commons_lang3_version"
  testImplementation "junit:junit:$junit_version"
  trafoCompileOnly project(path)
  taggingCompileOnly project(path)
  // use compileOnly as the tagging- and trafo-artifact are only an extension to the default artifact
}

task generate {}
task generateTR {}
task generateTagDef {}
task generateTagSchema {}


// one task per file
fileTree("src/main/grammars").matching { include '**/*.mc4' }.each {
  def g = it
  def taskname = "generateGrammar${it.getName().substring(0, it.getName().lastIndexOf('.'))}"
  def withDSTLGen = ("true").equals(getProperty('genTR')) && !["ODRuleGeneration", "ODRules", "TFBasisExts", "TFCommons",  // Do not generate TR grammars for TR-component grammars
                                                               "RegExType", "RegularExpressions", "MCStructuralTypes", // https://git.rwth-aachen.de/monticore/monticore/-/issues/3576
                                                               "Grammar", "Grammar_WithConcepts"].contains(it.getName().substring(0, it.getName().lastIndexOf('.')))
  def withTagGen = ("true").equals(getProperty('genTagging')) && !["TagSchema", "Tags", "Grammar", "Grammar_WithConcepts", "ODRuleGeneration", "ODRules", 
                                                                   "TFBasisExts", "TFCommons"].contains(it.getName().substring(0, it.getName().lastIndexOf('.')))
  task "$taskname"(type: MCTask) {
    grammar = file g
    outputDir = file grammarOutDir
    if (findProperty("ci") != null) {
      script = "de/monticore/monticore_noreports.groovy"
    }
    def grammarIncludingPackage = file("src/main/grammars").toURI().relativize(g.toURI()).toString()
    def uptoDate = incCheck(grammarIncludingPackage)
    outputs.upToDateWhen { uptoDate }
  }
  generate.dependsOn("$taskname")
  if (withDSTLGen) {   // run MC on the TR grammar
    task "${taskname}TR"(type: MCTask) {
      outputDir = file trafoOutDir // output into separate sourceset for the trafo artifact
      grammar = getTRFile(g, file(grammarOutDir))
      modelPath(grammarOutDir)
      modelPath(file("src/main/grammars").toString())
      if (findProperty("ci") != null) {
        script = "de/monticore/monticore_noreports.groovy"
      }
      def grammarIncludingPackage = file("src/main/grammars").toURI().relativize(g.toURI()).toString()
      def trGrammarIncludingPackage = file(grammarOutDir).toURI().relativize(file(grammar).toURI()).toString()
      outputs.upToDateWhen { incCheck(grammarIncludingPackage) }
      outputs.upToDateWhen { incCheck(trGrammarIncludingPackage) }
      isDSTL = true
      dependsOn(generate)
    }
    generateTR.dependsOn("${taskname}TR")
  }
  if (withTagGen) {   // run MC on the tag grammars
    task "${taskname}TagDef"(type: MCTask) {
      outputDir = file taggingOutDir
      grammar = getTagDefinitionFile(g, file(grammarOutDir))
      modelPath(grammarOutDir)
      modelPath(file("src/main/grammars").toString())
      if (findProperty("ci") != null) {
        script = "de/monticore/monticore_noreports.groovy"
      }
      def grammarIncludingPackage = file("src/main/grammars").toURI().relativize(g.toURI()).toString()
      def tagGrammarIncludingPackage = file(grammarOutDir).toURI().relativize(file(grammar).toURI()).toString()
      outputs.upToDateWhen { incCheck(grammarIncludingPackage) }
      outputs.upToDateWhen { incCheck(tagGrammarIncludingPackage) }
      genTag = true // Generate Tagging-Infrastructure
      dependsOn(generate)
    }
    generateTagDef.dependsOn("${taskname}TagDef")
    task "${taskname}TagSchema"(type: MCTask) {
      outputDir = file taggingOutDir
      grammar = getTagSchemaFile(g, file(grammarOutDir))
      modelPath(grammarOutDir)
      modelPath(file("src/main/grammars").toString())
      if (findProperty("ci") != null) {
        script = "de/monticore/monticore_noreports.groovy"
      }
      def grammarIncludingPackage = file("src/main/grammars").toURI().relativize(g.toURI()).toString()
      def tagGrammarIncludingPackage = file(grammarOutDir).toURI().relativize(file(grammar).toURI()).toString()
      outputs.upToDateWhen { incCheck(grammarIncludingPackage) }
      outputs.upToDateWhen { incCheck(tagGrammarIncludingPackage) }
      dependsOn(generate)
    }
    generateTagSchema.dependsOn("${taskname}TagSchema")
  }
}

// grammar dependencies without generated TR grammars
def grammarDependencies = ext {
  // no dependencies
  MCBasics = []
  MCLiteralsBasis = []
  Completeness = []
  MCStatementsBasis = []
  Antlr = []

  // one dependency
  MCSynchronizedStatements = ["MCCommonStatements"]
  CommonExpressions = ["ExpressionsBasis"]
  BitExpressions = ["ExpressionsBasis"]
  MCBasicTypes = ["MCBasics"]
  MCFullGenericTypes = ["MCSimpleGenericTypes"]
  ODRuleGeneration = ["MCFullGenericTypes"]
  BasicSymbols = ["MCBasics"]
  CompSymbols = ["BasicSymbols"]
  AssignmentExpressions = ["ExpressionsBasis"]
  UMLModifier = ["UMLStereotype"]
  MCArrayTypes = ["MCBasicTypes"]
  MCSimpleGenericTypes = ["MCCollectionTypes"]
  MCCommonStatements = ["MCVarDeclarationStatements"]
  MCCollectionTypes = ["MCBasicTypes"]
  StreamExpressions = ["CommonExpressions"]
  MCExceptionStatements = ["MCCommonStatements"]
  MCFunctionTypes = ["MCBasicTypes"]
  MCStructuralTypes = ["MCBasicTypes"]
  OOSymbols = ["BasicSymbols"]
  TFCommons = ["TFBasisExts"]
  MCJavaLiterals = ["MCCommonLiterals"]
  UMLStereotype = ["MCCommonLiterals"]
  MCArrayStatements = ["MCVarDeclarationStatements"]
  RegularExpressions = ["MCCommonLiterals"]

  // two dependencies
  Cardinality = ["MCBasics", "MCCommonLiterals"]
  ExpressionsBasis = ["MCBasics", "MCLiteralsBasis"]
  Grammar = ["MCCommonLiterals", "MCSimpleGenericTypes"]
  MCReturnStatements = ["MCStatementsBasis", "ExpressionsBasis"]
  MCCommonLiterals = ["MCBasics", "MCLiteralsBasis"]
  MCLowLevelStatements = ["MCStatementsBasis", "MCBasics"]
  MCAssertStatements = ["MCStatementsBasis", "ExpressionsBasis"]
  RegExType = ["RegularExpressions", "MCBasicTypes"]
  UglyExpressions = ["CommonExpressions", "MCBasicTypes"]

  // three dependencies
  LambdaExpressions = ["BasicSymbols", "MCBasicTypes", "ExpressionsBasis"]
  TFBasisExts = ["JavaLight", "MCSimpleGenericTypes", "MCCommonLiterals"]
  Tags = ["MCBasics", "BasicSymbols", "MCCommonLiterals"]
  TagSchema = ["MCBasics", "MCBasicTypes", "MCCommonLiterals"]

  // four dependencies
  JavaClassExpressions = ["CommonExpressions", "MCFullGenericTypes", "MCVarDeclarationStatements", "UglyExpressions"]
  MCCommon = ["Cardinality", "Completeness", "UMLModifier", "UMLStereotype"]
  ODRules = ["TFBasisExts", "MCCommonLiterals", "MCJavaLiterals", "UMLStereotype"]
  MCVarDeclarationStatements = ["MCStatementsBasis", "MCBasicTypes", "ExpressionsBasis", "OOSymbols"]

  // five dependencies
  JavaLight = ["AssignmentExpressions", "JavaClassExpressions", "MCCommonStatements", "MCArrayStatements", "MCReturnStatements"]

  // six dependencies
  MCFullJavaStatements = ["MCAssertStatements", "MCExceptionStatements", "MCLowLevelStatements", "MCReturnStatements", "MCSynchronizedStatements", "MCArrayStatements"]

  // nine dependencies
  Grammar_WithConcepts = ["Grammar", "MCCommonStatements", "MCReturnStatements", "MCExceptionStatements", "JavaClassExpressions", "JavaLight", "Antlr", "CommonExpressions", "BitExpressions"]
}

compileJava {
  tasks.findAll { task -> task.name.startsWith("generateGrammar") && !task.name.contains("TR") && !task.name.contains("TagDef") && !task.name.contains("TagSchema") }.each {
    def grammarName = it.getName().substring('generateGrammar'.length())
    def dependsOnGrammars = grammarDependencies[grammarName].collect { name -> tasks["generateGrammar${name}"] }
    it.dependsOn dependsOnGrammars
  }

  dependsOn project.collect { it.tasks.findAll { task -> task.name.startsWith("generateGrammar") } }
}

compileTestJava {
  dependsOn project.collect { it.tasks.withType(MCTask) }
}

compileJava.dependsOn generate
compileTrafoJava.dependsOn generateTR
compileTaggingJava.dependsOn generateTagDef
compileTaggingJava.dependsOn generateTagSchema

sourcesJar.dependsOn project.collect { it.tasks.withType(MCTask) }
jar.dependsOn(sourcesJar)

task generateTest {}

// one task per file
fileTree("src/test/grammars").matching { include '**/*.mc4' }.each {
  def g = it
  task "generateTest${it.getName().substring(0, it.getName().lastIndexOf('.'))}"(type: MCTask) {
    grammar = file g
    outputDir = file testGrammarOutDir
    handcodedPath "$projectDir/src/main/java", "$projectDir/src/test/java"
    modelPath "$projectDir/$grammarDir", "$projectDir/src/test/grammars", "$projectDir/src/main/examples"
    if (findProperty("ci") != null) {
      script = "de/monticore/monticore_noreports.groovy"
    }
    def grammarIncludingPackage = file("src/test/grammars").toURI().relativize(g.toURI()).toString()
    def uptoDate = incCheck(grammarIncludingPackage)
    outputs.upToDateWhen { uptoDate }
  }
  generateTest.dependsOn("generateTest${it.getName().substring(0, it.getName().lastIndexOf('.'))}")
}

fileTree("src/main/examples").matching { include '**/*.mc4' }.each {
  def g = it
  task "generateTest${it.getName().substring(0, it.getName().lastIndexOf('.'))}"(type: MCTask) {
    grammar = file g
    outputDir = file testGrammarOutDir
    handcodedPath "$projectDir/src/main/java", "$projectDir/src/test/java"
    modelPath "$projectDir/$grammarDir", "$projectDir/src/main/examples", "$projectDir/src/test/java"
    if (findProperty("ci") != null) {
      script = "de/monticore/monticore_noreports.groovy"
    }
    def grammarIncludingPackage = file("src/main/examples").toURI().relativize(g.toURI()).toString()
    def uptoDate = incCheck(grammarIncludingPackage)
    outputs.upToDateWhen { uptoDate }
  }
  generateTest.dependsOn("generateTest${it.getName().substring(0, it.getName().lastIndexOf('.'))}")
}

compileTestJava.dependsOn(generateTest)
processGrammarsResources.dependsOn(generateTR)
processGrammarsResources.dependsOn(generate)


/**
 * Integration with MLC language and tool
 */
task showArtifacts {}
task checkArtifacts {}
configurations { MLC }
dependencies {
  MLC(group: 'de.monticore.lang', name: 'mlc-gradle', version: "$version")
}
StringJoiner joiner = new StringJoiner(" ")
configurations.compileClasspath.resolve().each { joiner.add(it.toString()) }
joiner.add "$projectDir/target/symbols"
String mp = joiner.toString()

// two tasks per MLC file
// each task needs to depend on all internally promoted MLCs
fileTree("src").matching { include '**/*.mlc' }.each {
  def f = it
  def mlcName = it.getName().substring(0, it.getName().lastIndexOf('.'))

  task "showArtifacts${mlcName}"(type: JavaExec) {
    classpath = configurations.MLC
    group = 'MLC'
    mainClass = 'de.monticore.mlc.MLCTool'
    args "-input", f, "-projectDir", projectDir, "-mp", mp, "-s", "-reports", "${buildDir}/reports", "-a", "-noFQ"
    dependsOn("generateGrammar${mlcName}")
    showArtifacts.dependsOn("showArtifacts${mlcName}")
  }

  task "checkArtifacts${mlcName}"(type: JavaExec) {
    classpath = configurations.MLC
    group = 'MLC'
    mainClass = 'de.monticore.mlc.MLCTool'
    args "-input", f, "-projectDir", projectDir, "-mp", mp, "-s", "-check", "-reports", "${buildDir}/reports"
    dependsOn("generateGrammar${mlcName}")
    checkArtifacts.dependsOn("checkArtifacts${mlcName}")
  }
}

///////////// dependencies between check artifacts tasks ///////////////////////

checkArtifactsAssignmentExpressions.dependsOn(checkArtifactsExpressionsBasis)
checkArtifactsBitExpressions.dependsOn(checkArtifactsExpressionsBasis)
checkArtifactsCommonExpressions.dependsOn(checkArtifactsExpressionsBasis)
checkArtifactsExpressionsBasis.dependsOn(checkArtifactsMCBasics)
checkArtifactsExpressionsBasis.dependsOn(checkArtifactsMCLiteralsBasis)
checkArtifactsJavaClassExpressions.dependsOn(checkArtifactsCommonExpressions)
checkArtifactsJavaClassExpressions.dependsOn(checkArtifactsMCFullGenericTypes)

checkArtifactsAntlr.dependsOn(checkArtifactsMCBasics)
checkArtifactsGrammar.dependsOn(checkArtifactsMCCommonLiterals)
checkArtifactsGrammar.dependsOn(checkArtifactsMCSimpleGenericTypes)
checkArtifactsGrammar_WithConcepts.dependsOn(checkArtifactsGrammar)
checkArtifactsGrammar_WithConcepts.dependsOn(checkArtifactsMCCommonStatements)
checkArtifactsGrammar_WithConcepts.dependsOn(checkArtifactsMCReturnStatements)
checkArtifactsGrammar_WithConcepts.dependsOn(checkArtifactsMCExceptionStatements)
checkArtifactsGrammar_WithConcepts.dependsOn(checkArtifactsJavaClassExpressions)
checkArtifactsGrammar_WithConcepts.dependsOn(checkArtifactsJavaLight)
checkArtifactsGrammar_WithConcepts.dependsOn(checkArtifactsAntlr)
checkArtifactsGrammar_WithConcepts.dependsOn(checkArtifactsCommonExpressions)
checkArtifactsGrammar_WithConcepts.dependsOn(checkArtifactsBitExpressions)

checkArtifactsMCCommonLiterals.dependsOn(checkArtifactsMCLiteralsBasis)
checkArtifactsMCLiteralsBasis.dependsOn(checkArtifactsMCBasics)
checkArtifactsMCJavaLiterals.dependsOn(checkArtifactsMCCommonLiterals)

checkArtifactsMCArrayStatements.dependsOn(checkArtifactsMCVarDeclarationStatements)
checkArtifactsMCAssertStatements.dependsOn(checkArtifactsMCStatementsBasis)
checkArtifactsMCAssertStatements.dependsOn(checkArtifactsExpressionsBasis)
checkArtifactsMCCommonStatements.dependsOn(checkArtifactsMCVarDeclarationStatements)
checkArtifactsMCExceptionStatements.dependsOn(checkArtifactsMCCommonStatements)
checkArtifactsMCFullJavaStatements.dependsOn(checkArtifactsMCAssertStatements)
checkArtifactsMCFullJavaStatements.dependsOn(checkArtifactsMCExceptionStatements)
checkArtifactsMCFullJavaStatements.dependsOn(checkArtifactsMCLowLevelStatements)
checkArtifactsMCFullJavaStatements.dependsOn(checkArtifactsMCReturnStatements)
checkArtifactsMCFullJavaStatements.dependsOn(checkArtifactsMCSynchronizedStatements)
checkArtifactsMCFullJavaStatements.dependsOn(checkArtifactsMCArrayStatements)
checkArtifactsMCLowLevelStatements.dependsOn(checkArtifactsMCStatementsBasis)
checkArtifactsMCReturnStatements.dependsOn(checkArtifactsMCStatementsBasis)
checkArtifactsMCReturnStatements.dependsOn(checkArtifactsExpressionsBasis)
checkArtifactsMCStatementsBasis.dependsOn(checkArtifactsMCBasics)
checkArtifactsMCSynchronizedStatements.dependsOn(checkArtifactsMCCommonStatements)
checkArtifactsMCVarDeclarationStatements.dependsOn(checkArtifactsMCStatementsBasis)
checkArtifactsMCVarDeclarationStatements.dependsOn(checkArtifactsMCBasicTypes)
checkArtifactsMCVarDeclarationStatements.dependsOn(checkArtifactsExpressionsBasis)
checkArtifactsMCVarDeclarationStatements.dependsOn(checkArtifactsOOSymbols)

checkArtifactsBasicSymbols.dependsOn(checkArtifactsMCBasics)
checkArtifactsOOSymbols.dependsOn(checkArtifactsBasicSymbols)

checkArtifactsODRuleGeneration.dependsOn(checkArtifactsMCFullGenericTypes)
checkArtifactsODRules.dependsOn(checkArtifactsMCFullGenericTypes)
checkArtifactsODRules.dependsOn(checkArtifactsTFBasisExts)
checkArtifactsODRules.dependsOn(checkArtifactsMCJavaLiterals)
checkArtifactsODRules.dependsOn(checkArtifactsUMLStereotype)
checkArtifactsTFBasisExts.dependsOn(checkArtifactsMCSimpleGenericTypes)
checkArtifactsTFBasisExts.dependsOn(checkArtifactsMCCommonLiterals)
checkArtifactsTFBasisExts.dependsOn(checkArtifactsJavaLight)
checkArtifactsTFCommons.dependsOn(checkArtifactsTFBasisExts)

checkArtifactsMCArrayTypes.dependsOn(checkArtifactsMCBasicTypes)
checkArtifactsMCBasicTypes.dependsOn(checkArtifactsMCBasics)
checkArtifactsMCBasicTypes.dependsOn(checkArtifactsOOSymbols)
checkArtifactsMCCollectionTypes.dependsOn(checkArtifactsMCBasicTypes)
checkArtifactsMCFullGenericTypes.dependsOn(checkArtifactsMCSimpleGenericTypes)
checkArtifactsMCSimpleGenericTypes.dependsOn(checkArtifactsMCCollectionTypes)

checkArtifactsCardinality.dependsOn(checkArtifactsMCBasics)
checkArtifactsCardinality.dependsOn(checkArtifactsMCCommonLiterals)
checkArtifactsJavaLight.dependsOn(checkArtifactsAssignmentExpressions)
checkArtifactsJavaLight.dependsOn(checkArtifactsJavaClassExpressions)
checkArtifactsJavaLight.dependsOn(checkArtifactsMCCommonStatements)
checkArtifactsJavaLight.dependsOn(checkArtifactsMCArrayStatements)
checkArtifactsJavaLight.dependsOn(checkArtifactsMCReturnStatements)
checkArtifactsMCCommon.dependsOn(checkArtifactsCardinality)
checkArtifactsMCCommon.dependsOn(checkArtifactsCompleteness)
checkArtifactsMCCommon.dependsOn(checkArtifactsUMLModifier)
checkArtifactsMCCommon.dependsOn(checkArtifactsUMLStereotype)
checkArtifactsUMLModifier.dependsOn(checkArtifactsUMLStereotype)
checkArtifactsUMLStereotype.dependsOn(checkArtifactsMCCommonLiterals)

checkArtifactsRegularExpressions.dependsOn(checkArtifactsMCCommonLiterals)

checkArtifactsRegExType.dependsOn(checkArtifactsRegularExpressions)

checkArtifactsUglyExpressions.dependsOn(checkArtifactsCommonExpressions)
checkArtifactsUglyExpressions.dependsOn(checkArtifactsMCBasicTypes)

///////////// dependencies between show artifacts tasks ///////////////////////

showArtifactsAssignmentExpressions.dependsOn(showArtifactsExpressionsBasis)
showArtifactsBitExpressions.dependsOn(showArtifactsExpressionsBasis)
showArtifactsCommonExpressions.dependsOn(showArtifactsExpressionsBasis)
showArtifactsExpressionsBasis.dependsOn(showArtifactsMCBasics)
showArtifactsExpressionsBasis.dependsOn(showArtifactsMCLiteralsBasis)
showArtifactsJavaClassExpressions.dependsOn(showArtifactsCommonExpressions)
showArtifactsJavaClassExpressions.dependsOn(showArtifactsMCFullGenericTypes)

showArtifactsAntlr.dependsOn(showArtifactsMCBasics)
showArtifactsGrammar.dependsOn(showArtifactsMCCommonLiterals)
showArtifactsGrammar.dependsOn(showArtifactsMCSimpleGenericTypes)
showArtifactsGrammar_WithConcepts.dependsOn(showArtifactsGrammar)
showArtifactsGrammar_WithConcepts.dependsOn(showArtifactsMCCommonStatements)
showArtifactsGrammar_WithConcepts.dependsOn(showArtifactsMCReturnStatements)
showArtifactsGrammar_WithConcepts.dependsOn(showArtifactsMCExceptionStatements)
showArtifactsGrammar_WithConcepts.dependsOn(showArtifactsJavaClassExpressions)
showArtifactsGrammar_WithConcepts.dependsOn(showArtifactsJavaLight)
showArtifactsGrammar_WithConcepts.dependsOn(showArtifactsAntlr)
showArtifactsGrammar_WithConcepts.dependsOn(showArtifactsCommonExpressions)
showArtifactsGrammar_WithConcepts.dependsOn(showArtifactsBitExpressions)

showArtifactsMCCommonLiterals.dependsOn(showArtifactsMCLiteralsBasis)
showArtifactsMCLiteralsBasis.dependsOn(showArtifactsMCBasics)
showArtifactsMCJavaLiterals.dependsOn(showArtifactsMCCommonLiterals)

showArtifactsMCArrayStatements.dependsOn(showArtifactsMCVarDeclarationStatements)
showArtifactsMCAssertStatements.dependsOn(showArtifactsMCStatementsBasis)
showArtifactsMCAssertStatements.dependsOn(showArtifactsExpressionsBasis)
showArtifactsMCCommonStatements.dependsOn(showArtifactsMCVarDeclarationStatements)
showArtifactsMCExceptionStatements.dependsOn(showArtifactsMCCommonStatements)
showArtifactsMCFullJavaStatements.dependsOn(showArtifactsMCAssertStatements)
showArtifactsMCFullJavaStatements.dependsOn(showArtifactsMCExceptionStatements)
showArtifactsMCFullJavaStatements.dependsOn(showArtifactsMCLowLevelStatements)
showArtifactsMCFullJavaStatements.dependsOn(showArtifactsMCReturnStatements)
showArtifactsMCFullJavaStatements.dependsOn(showArtifactsMCSynchronizedStatements)
showArtifactsMCFullJavaStatements.dependsOn(showArtifactsMCArrayStatements)
showArtifactsMCLowLevelStatements.dependsOn(showArtifactsMCStatementsBasis)
showArtifactsMCReturnStatements.dependsOn(showArtifactsMCStatementsBasis)
showArtifactsMCReturnStatements.dependsOn(showArtifactsExpressionsBasis)
showArtifactsMCStatementsBasis.dependsOn(showArtifactsMCBasics)
showArtifactsMCSynchronizedStatements.dependsOn(showArtifactsMCCommonStatements)
showArtifactsMCVarDeclarationStatements.dependsOn(showArtifactsMCStatementsBasis)
showArtifactsMCVarDeclarationStatements.dependsOn(showArtifactsMCBasicTypes)
showArtifactsMCVarDeclarationStatements.dependsOn(showArtifactsExpressionsBasis)
showArtifactsMCVarDeclarationStatements.dependsOn(showArtifactsOOSymbols)

showArtifactsBasicSymbols.dependsOn(showArtifactsMCBasics)
showArtifactsOOSymbols.dependsOn(showArtifactsBasicSymbols)

showArtifactsODRuleGeneration.dependsOn(showArtifactsMCFullGenericTypes)
showArtifactsODRules.dependsOn(showArtifactsMCFullGenericTypes)
showArtifactsODRules.dependsOn(showArtifactsTFBasisExts)
showArtifactsODRules.dependsOn(showArtifactsMCJavaLiterals)
showArtifactsODRules.dependsOn(showArtifactsUMLStereotype)
showArtifactsTFBasisExts.dependsOn(showArtifactsMCSimpleGenericTypes)
showArtifactsTFBasisExts.dependsOn(showArtifactsMCCommonLiterals)
showArtifactsTFBasisExts.dependsOn(showArtifactsJavaLight)
showArtifactsTFCommons.dependsOn(showArtifactsTFBasisExts)

showArtifactsMCArrayTypes.dependsOn(showArtifactsMCBasicTypes)
showArtifactsMCBasicTypes.dependsOn(showArtifactsMCBasics)
showArtifactsMCBasicTypes.dependsOn(showArtifactsOOSymbols)
showArtifactsMCCollectionTypes.dependsOn(showArtifactsMCBasicTypes)
showArtifactsMCFullGenericTypes.dependsOn(showArtifactsMCSimpleGenericTypes)
showArtifactsMCSimpleGenericTypes.dependsOn(showArtifactsMCCollectionTypes)

showArtifactsCardinality.dependsOn(showArtifactsMCBasics)
showArtifactsCardinality.dependsOn(showArtifactsMCCommonLiterals)
showArtifactsJavaLight.dependsOn(showArtifactsAssignmentExpressions)
showArtifactsJavaLight.dependsOn(showArtifactsJavaClassExpressions)
showArtifactsJavaLight.dependsOn(showArtifactsMCCommonStatements)
showArtifactsJavaLight.dependsOn(showArtifactsMCArrayStatements)
showArtifactsJavaLight.dependsOn(showArtifactsMCReturnStatements)
showArtifactsMCCommon.dependsOn(showArtifactsCardinality)
showArtifactsMCCommon.dependsOn(showArtifactsCompleteness)
showArtifactsMCCommon.dependsOn(showArtifactsUMLModifier)
showArtifactsMCCommon.dependsOn(showArtifactsUMLStereotype)
showArtifactsUMLModifier.dependsOn(showArtifactsUMLStereotype)
showArtifactsUMLStereotype.dependsOn(showArtifactsMCCommonLiterals)

showArtifactsRegularExpressions.dependsOn(showArtifactsMCCommonLiterals)

showArtifactsRegExType.dependsOn(showArtifactsRegularExpressions)

showArtifactsUglyExpressions.dependsOn(showArtifactsExpressionsBasis)
showArtifactsUglyExpressions.dependsOn(showArtifactsMCBasicTypes)

java {
  registerFeature('grammars') {
    usingSourceSet(sourceSets.grammars)
  }
  registerFeature('tests') {
    usingSourceSet(sourceSets.test)
  }
}

shadowJar {
  from sourceSets.grammars.resources
}

shadowJar.dependsOn(generate)
shadowJar.dependsOn(generateTest)

jar.dependsOn(shadowJar)
jar.dependsOn(grammarsJar)

if (findProperty('ci') == null) {
  jar.dependsOn(shadowJar, testJar)
}

if (("true").equals(getProperty('genTR'))) {
  // Bundle and publish the generated TR artifacts separately as monticore-grammar-trafo
  tasks.register('trafoJar', Jar) {
    from sourceSets.trafo.output
    group 'build'
  }
  tasks.register('trafoSourcesJar', Jar) {
    from sourceSets.trafo.java
    group 'build'
  }
  publishing {
    publications {
      "trafo"(MavenPublication) {
        artifactId = project.name + "-trafo"
        artifact tasks.trafoJar { appendix 'trafo' }
        artifact tasks.trafoSourcesJar { appendix 'trafo'; classifier 'sources' }
      }
    }
  }
  // And expose the trafo jar artifact to other subprojects using a 'trafo' configuration
  configurations {
    trafo
  }
  artifacts {
    trafo(trafoJar)
  }
}
if (("true").equals(getProperty('genTagging'))) {
  // Bundle and publish the generated tagging artifacts separately as monticore-grammar-tagging
  tasks.register('taggingJar', Jar) {
    from sourceSets.tagging.output
    group 'build'
  }
  tasks.register('taggingSourcesJar', Jar) {
    from sourceSets.tagging.java
    group 'build'
  }
  publishing {
    publications {
      "tagging"(MavenPublication) {
        artifactId = project.name + "-tagging"
        artifact tasks.taggingJar { appendix 'tagging' }
        artifact tasks.taggingSourcesJar { appendix 'tagging'; classifier 'sources' }
      }
    }
  }
  configurations {
    tagging
  }
  artifacts {
    tagging(taggingJar)
  }
}
