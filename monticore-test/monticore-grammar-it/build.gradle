/* (c) https://github.com/MontiCore/monticore */
import de.monticore.MCTask

plugins{
  id "de.monticore.generator"  //Monticore Plugin
  id "jacoco"
}

description = 'MontiCore Grammars Main Integration Test'

ext.grammarDir = 'src/main/grammars'

configurations {grammar}

dependencies {
  implementation project(':monticore-runtime')
  implementation project(':monticore-grammar')
  implementation "de.se_rwth.commons:se-commons-utilities:$se_commons_version"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
  testImplementation "org.junit.vintage:junit-vintage-engine:$junit_version"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
  grammar (project(path: ':monticore-grammar')){
    capabilities {
      requireCapability("de.monticore:monticore-grammar-grammars")
    }
  }
}

buildDir = file("$projectDir/target")

// The grammars of this subproject are built by the generateMCGrammars MCGenTask

compileJava {
  dependsOn project.collect { it.tasks.withType(MCTask) }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(11)
  }
}

tasks.withType(JavaCompile) {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
  options.encoding = 'UTF-8'
  options.deprecation false
  options.warnings = false
}

task testReport(type: TestReport) {
  destinationDirectory = file("$buildDir/reports/allTests")
  // Include the results from the 'test' task
  testResults.from(tasks.withType(Test).each({it.getBinaryResultsDirectory()}))
}