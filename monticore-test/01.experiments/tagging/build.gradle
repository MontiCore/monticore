/* (c) https://github.com/MontiCore/monticore */
description = 'Experiments: tagging'

// The grammars of this subproject are built by the generateMCGrammars MCGenTask

// Additional (tagging) tests require TagDef/TagSchema grammars.
// Those tests are present in a withGen sourceSet
// Only run these tests if genTagging is true
if (("true").equals(getProperty('genTagging'))) {
  sourceSets {
    withGen
  }

  // Two MCGenTasks, one for the TagDef (with genTag=true) & one for the TagSchema
  var tagDefOutput = project.layout.buildDirectory.dir("generated-withSchemaSources/monticore/tagdef")
  var tagSchemaOutput = project.layout.buildDirectory.dir("generated-withSchemaSources/monticore/tagschema")
  tasks.register("generateTagDefGrammars", MCGenTask) {
    var generateMCGrammars = tasks.named("generateMCGrammars", MCGenTask).get()
    grammar.setFrom(generateMCGrammars.getTagDefOutput())
    outputDir = tagDefOutput
    modelPath.from(generateMCGrammars.getOutputDir()) // the directory hierarchy is required here
    if (findProperty("ci") != null) {
      script = "de/monticore/monticore_noreports.groovy" // noreports workflow script
    }
    genTag = true
    dependsOn("generateMCGrammars")
    symbolPathConfiguration.from(configurations.named("withGenGrammarSymbolDependencies"))
  }
  tasks.register("generateTagSchemaGrammars", MCGenTask) {
    var generateMCGrammars = tasks.named("generateMCGrammars", MCGenTask).get()
    grammar.setFrom(generateMCGrammars.getTagSchemaOutput())
    outputDir = tagSchemaOutput
    modelPath.from(generateMCGrammars.getOutputDir()) // the directory hierarchy is required here
    if (findProperty("ci") != null) {
      script = "de/monticore/monticore_noreports.groovy" // noreports workflow script
    }
    dependsOn("generateMCGrammars")
    symbolPathConfiguration.from(configurations.named("withGenGrammarSymbolDependencies"))
  }
  // define the withGen sourceSet
  sourceSets {
    withGen {
      java.srcDirs += tasks.generateTagDefGrammars.getOutputDir()
      java.srcDirs += tasks.generateTagSchemaGrammars.getOutputDir()
      compileClasspath += sourceSets.main.output
      runtimeClasspath += sourceSets.main.output
    }
  }

  configurations {
    withGenImplementation.extendsFrom implementation
    withGenRuntimeOnly.extendsFrom runtimeOnly
  }

  dependencies {
    withGenImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    withGenImplementation "org.junit.vintage:junit-vintage-engine:$junit_version"
    withGenRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    // monticore-grammar-tagging
    withGenImplementation files(project(":monticore-grammar").sourceSets.tagging.output.classesDirs)
    withGenImplementation project(project.path)
    withGenGrammarSymbolDependencies files(project(":monticore-grammar").layout.buildDirectory.dir("generated-sources/monticore/sourcecode"))
    withGenGrammarSymbolDependencies files(project(":monticore-grammar").sourceSets.main.grammars.getSourceDirectories())
  }

  // Register a jUnit platform
  tasks.register('withGenTest', Test) {
    description = 'Runs tests with tagGen.'
    group = 'verification'

    testClassesDirs = sourceSets.withGen.output.classesDirs
    classpath = sourceSets.withGen.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

  }

  check.dependsOn withGenTest

  compileWithGenJava.dependsOn(tasks.named("generateTagSchemaGrammars"))
  compileWithGenJava.dependsOn(tasks.named("generateTagDefGrammars"))

}
