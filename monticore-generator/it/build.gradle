/* (c) https://github.com/MontiCore/monticore */
import de.monticore.MCTask

plugins {
  id "de.set.ecj" version "1.4.1" // Eclipse compiler as it's much faster than javac
  id 'java'
  id "monticore" version "6.1.0.1"
}

group = 'de.monticore'
version = '6.1.0.1'
description = 'MontiCore Generator Main Integration Test'
sourceCompatibility = '1.8'

ext.grammarDir = 'src/main/grammars'
ext.grammar_version = '6.1.0.1'
ext.mc_version = '6.1.0.1'
ext.junit_version = '4.12'

configurations {grammar}

dependencies {
  implementation group:'de.monticore', name:'javaDSL', version:'4.3.17-SNAPSHOT'
  implementation group:'de.monticore', name:'monticore-runtime', version:grammar_version
  implementation group:'de.monticore', name:'monticore-generator', version:mc_version
  testImplementation group:'junit', name:'junit', version:junit_version
  grammar group:'de.monticore', name:'monticore-grammar', version:grammar_version, classifier:"grammars"
}

repositories {
  mavenLocal()
  maven {
    credentials.username mavenUser
    credentials.password mavenPassword
    url repo
  }
}

if (!hasProperty('buildProfile')) ext.buildProfile = 'default'  // chooses the profile

apply from: "profile-${buildProfile}.gradle"  // includes the corresponding build file

tasks.withType(JavaCompile) {
  options.warnings = false
  options.encoding = 'UTF-8'
}

compileJava {
  dependsOn project.collect { it.tasks.withType(MCTask) }
}

// Fixes issue with java plugin in projects without resources
compileTestJava.doFirst { mkdir sourceSets.main.output.resourcesDir }

test {
  testLogging {
    showStandardStreams = true
  }
  
}
