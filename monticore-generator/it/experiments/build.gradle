/* (c) https://github.com/MontiCore/monticore */
import de.monticore.MCTask
plugins {
  id "monticore" version "5.3.0-SNAPSHOT" // MontiCore Plugin
}
allprojects {
  group = 'de.monticore'
  version = '5.3.0-SNAPSHOT'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'monticore'
  
  buildDir = file("$projectDir/target")
  
  repositories {
    mavenLocal()
    maven {
      credentials.username mavenUser
      credentials.password mavenPassword
      url repo
    }
  }
  
  ext.grammarDir = 'src/main/grammars'
  ext.grammar_version = '5.3.0-SNAPSHOT'
  ext.mc_version = '5.3.0-SNAPSHOT'
  ext.junit_version = '4.12'
  ext.outDir = "$buildDir/generated-sources/monticore/sourcecode"
  
  dependencies {
    implementation group:'de.monticore', name:'monticore-runtime', version:grammar_version
    implementation group:'de.monticore', name:'monticore-grammar', version:grammar_version
    testImplementation group:'junit', name:'junit', version:junit_version
    grammar group:'de.monticore', name:'monticore-grammar', version:grammar_version, classifier:"grammars"
    grammar group:'de.monticore', name:'monticore-runtime', version:grammar_version
  }
  
  sourceSets {
    main.java.srcDirs += [ outDir ]
  }
  
  
  sourceCompatibility = '1.8'
  
  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.deprecation false
  }
  
  compileJava {
    dependsOn project.collect { it.tasks.withType(MCTask) }
  }

}
