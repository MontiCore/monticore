/* (c) https://github.com/MontiCore/monticore */
plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id 'maven-publish'
//  id 'cz.malohlava.visteg' version '1.0.5'  // produces a task graph visualization
}
version = "5.4.0-SNAPSHOT"
group = "de.monticore"

repositories {
  maven {
    credentials.username mavenUser
    credentials.password mavenPassword
    url repo
  }
  mavenLocal()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile group: 'de.monticore', name: 'monticore-cli', version: "$version"
  compile group: 'de.monticore', name: 'monticore-generator', version: "$version"
}

gradlePlugin.plugins {
  monticore {
    id = 'monticore'
    implementationClass = 'de.monticore.MCPlugin'
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

// configure deployment
publishing {
  // configure what artifacts to publish
  publications {
    pluginMaven(MavenPublication){
      artifact sourcesJar
    }
  }
  repositories.maven {
    credentials.username mavenUser
    credentials.password mavenPassword
    def releasesRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-releases/"
    def snapshotsRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-snapshots/"
    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
  }
}
