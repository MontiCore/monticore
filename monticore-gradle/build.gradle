/* (c) https://github.com/MontiCore/monticore */

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation;
plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id 'maven-publish'
  id 'com.github.johnrengelman.shadow' version '6.0.0'
//  id 'cz.malohlava.visteg' version '1.0.5'  // produces a task graph visualization
}
version = "6.4.0-SNAPSHOT"
group = "de.monticore"

repositories {
  if(("true").equals(getProperty('useLocalRepo'))){
    mavenLocal()
  }
  maven {
    credentials.username mavenUser
    credentials.password mavenPassword
    url repo
  }
}

dependencies {
  shadow localGroovy()
  shadow gradleApi()
  implementation group: 'de.monticore', name: 'monticore-cli', version: "$version"
  implementation group: 'de.monticore', name: 'monticore-generator', version: "$version"
}

shadowJar{
  classifier = ''
}

jar.enabled(false)

gradlePlugin.plugins {
  monticore {
    id = 'monticore'
    implementationClass = 'de.monticore.MCPlugin'
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier = 'sources'
  from sourceSets.main.allSource
}

// configure deployment
publishing {
  // configure what artifacts to publish
  publications {
    shadow(MavenPublication) { publication ->
      project.shadow.component(publication)
    }
  }
  repositories.maven {
    credentials.username mavenUser
    credentials.password mavenPassword
    def releasesRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-releases/"
    def snapshotsRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/monticore-snapshots/"
    url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
  }
}
